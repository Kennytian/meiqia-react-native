buildscript {
    repositories {
        jcenter()
        google()
        maven {
            url 'https://dl.bintray.com/android/android-tools'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    def packageJson = new groovy.json.JsonSlurper().parseText(file('../package.json').text)
    compileSdkVersion safeExtGet("compileSdkVersion", 25)
    buildToolsVersion safeExtGet("buildToolsVersion", '25.0.3')
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', 25)
        versionCode 1
        versionName packageJson.version
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation 'com.facebook.react:react-native:+'
    implementation 'com.meiqia:meiqiasdk:3.6.0'
    implementation "com.android.support:support-v4:${safeExtGet('supportLibVersion', '25.3.1')}"
    implementation 'com.squareup.okhttp3:okhttp:3.5.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
}
